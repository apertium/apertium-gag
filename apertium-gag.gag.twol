Alphabet

    A Ä Â B C Ç D E Ê F G H I İ J K L M N O Ö P R S Ş Ș T Ţ Ț U Ü V Y Z
    a ä â b c ç d e ê f g h ı i j k l m n o ö p r s ş ș t ţ ț u ü v y z

 %{A%}:a %{A%}:e %{A%}:ä  %{A%}:ê %{A%}:â
 %{E%}:ê %{E%}:e %{E%}:y  %{E%}:i  %{E%}:u
 %{I%}:i 
 %{й%}:y %{й%}:0
 %{i%}:u %{i%}:i %{i%}:ı %{i%}:ü %{i%}:0
 %{y%}:u %{y%}:i %{y%}:ı %{y%}:ü %{y%}:0
 %{s%}:s %{s%}:0 %{T%}:t
 %{n%}:n %{n%}:0
 %{D%}:d 
 %{L%}:l 
 %{☭%}:0
 %{в%}:0
 %>:0  
 %+ % ;
 

Sets

Cns = B C Ç D  F G H  J K L M N  P R S Ş Ș T Ţ Ț V Y Z
      b c ç d  f g h  j k l m n  p r s ş ș t ţ ț v y z ;

Vowel = A E Ê Ä I O Ö U Ü  
        a e ê ä i o ö u ü   ;

HardVow = A Â O I U
          a â o ı u ;

SoftVow = E Ä İ Ö Ü
          e ä i ö ü ;

RoundVow = O Ö U Ü
           o ö u ü ;

FlatVow = A Â E İ I 
          a â e i ı ;
 
 
NasalCns = n m 
           N M ;

CnsVcls   = F S T K Ç Ş H P 
            f s t k ç ş h p ;


            



Rules 

"Delete morpheme boundary"
%>:0 <=> _ ;

"{D} changed to t when after a stem-final voiceless consonant" 

%{D%}:t <=> :CnsVcls %>:0 _ ;    

"{T} change to d before vowels "

%{T%}:d  <=>  _  %>:0  :Vowel  ;  

"Vowel harmony for archiphoneme {I}"

%{I%}:Vy <=> :Vx :Cns+/:0* _ ;
     where Vx in ( a â ä e ê o ö u ü ı i )
           Vy in ( ı ı i i ı u ü u ü ı i )
     matched ;

"{й}  become y when intervocalic"
%{й%}:y <=> :Vowel :0 _ :Vowel ;

"{L} changed to n when after a Nasal consonant"
%{L%}:n <=> :NasalCns  %>:0 _ ; 

!"{K} change to a before vowels"
!%{K%}:0 <=>  _  %>:0 :Vowel ;

"Voicing of non-coronal stem-final voiceless consonants"

Cx:Cy <=> :Vowel/:0* _ %>: :Vowel/:0* ;
      except
                     _ %{☭%}: %>: ; 
      where Cx in ( p k t )
            Cy in ( b g d )
      matched ;



! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

