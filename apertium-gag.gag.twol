Alphabet

    A Ä Â B C Ç D E Ê F G H I İ J K L M N O Ö P R S Ş Ș T Ţ Ț U Ü V Y Z
    a ä â b c ç d e ê f g h ı i j k l m n o ö p r s ş ș t ţ ț u ü v y z

 %{A%}:a %{A%}:ä  %{A%}:ê %{A%}:â
    
 %{E%}:ê %{E%}:e %{E%}:y  %{E%}:i  %{E%}:u
 %{I%}:i  
 %{й%}:y %{й%}:0
 %{i%}:i  %{i%}:0 ! %{i%}:u  !%{i%}:ı !%{i%}:ü 
 %{y%}:u %{y%}:i %{y%}:ı %{y%}:ü %{y%}:0
 %{s%}:s %{s%}:0 
 %{T%}:t %{T%}:d
 %{n%}:n  %{n%}:0
 %{D%}:d %{k%}:k
 %{L%}:l %{u%}:u 
 %{☭%}:0 
 %{в%}:0
 %{e%}:e
 %{a%}:a %{a%}:e
 %>:0
 %{N%}:n %{N%}:0
 %+ % ;
 

Sets

Cns = B C Ç D  F G H  J K L M N  P R S Ş Ș T Ţ Ț V Y Z
      b c ç d  f g h  j k l m n  p r s ş ș t ţ ț v y z ;

Vowel = A E Ê Ä İ I O Ö U Ü  
        a e ê ä i ı o ö u ü   ;

HardVow = A Â O I U
          a â o ı u ;

SoftVow = E Ä İ Ö Ü
          e ä i ö ü ;

RoundVow = O Ö U Ü
           o ö u ü ;

FlatVow = A Â E İ I 
          a â e i ı ;
 
NasalCns = n m 
           N M ;

CnsVcls   = F S T K Ç Ş H P 
            f s t k ç ş h p ;

ArchiCns = %{k%} ; 

            



Rules 

"Delete morpheme boundary"
%>: <=> _ ;

"{D} changed to t when after a stem-final voiceless consonant" 

%{D%}:t <=> :CnsVcls %>: _ ; 
   

"{T} change to d before vowels "

%{T%}:d  <=>  _  %>: (:0) :Vowel  ; 
 
!@ g:g i:i {T}:d >:0 {й}:0 {a}:e c:c {A}:ä >:0 m:m

"Vowel harmony for archiphoneme {I}"

%{I%}:Vy <=> :Vx  [ :Cns | %{k%}: ]+/:0* _ ; 
    where Vx in ( a â ä e ê o ö u ü ı i )
          Vy in ( ı ı i i ı u ü u ü ı i )
    matched ;

"Vowel harmony for archiphoneme {A}"
%{A%}:Vy <=> :Vx  [ :Cns | %{k%}: ]+/:0* _ ; 
    except 
        %{a%}: c %{e%}: k %>: %{L%}: _ ;
    where Vx in ( a ä e ê o ö u ü ı i )
              Vy in ( a ä ä a a ä a ä a ä )
    matched ;

!@ o:o l:l >:0 {й}:0 {A}:a c:c >:0 {e}:e k:k >:0 {L}:l {A}:a r:r

"Vowel harmony in the future suffix with plural"
%{A%}:Vy <=> %{a%}:Vx c %{e%}: k %>: %{L%}: _ ; 
    where Vx in ( a ä e ê )
          Vy in ( a ä ä a )
    matched ;
 

!%{A%}:Vy <=> :Vx  [ :Cns | %{k%}: ]+/:0* _ ; 
!    where Vx in ( a ä e ê o ö u ü ı i )
!          Vy in ( a ä ä a a ä a ä a ä )
!    matched ;
!

"Vowel harmony for archiphoneme {a}"
%{a%}:Vy <=> :Vx  [ :Cns | %{k%}: ]+/:0* _ ; 
    where Vx in ( a ä e ê o ö u ü ı i )
              Vy in ( a e e a a e a e a e )
    matched ;


"{й}  become y when intervocalic"
%{й%}:y <=> :Vowel :0 _ :Vowel ;

"{s} become 0 when intervocalic"
%{s%}:0 <=> [ ArchiCns: | Cns: ] %>: _ :Vowel ;

!@ ç:ç o:o c:c u:u {k}:0 >:0 {s}:0 {I}:u {n}:0 >:0 {N}:n {I}:u n:n

"{L} changed to n when after a Nasal consonant"
%{L%}:n <=> :NasalCns  %>: _ ; 

"deleting k before vowels"
%{k%}:0 <=> :Vowel _ %>: :0* :Vowel ;

" u become a before vowels"
u:a <=> : _ %{k%}:0 >:0 :0* :a ;
       
"delete {n} before genetive case "
%{n%}:0 <=>  _ %>: %{N%}:  ;

"{n} become 0 at the end "
%{n%}:0 <=>  _ .#. ;

"No {N} after consonant"
%{N%}:0 <=> [ ArchiCns: | Cns: ] %>: _ ;

!@ a:a y:y >:0 {N}:0 {I}:ı n:n
!@ ş:ş k:k o:o l:l a:a >:0 {N}:n {I}:ı n:n

!@ ç:ç o:o c:c u:u {k}:0 >:0 {N}:0 {I}:u n:n

!@ ç:ç o:o c:c u:u {k}:0 >:0 {s}:0 {I}:u {n}:0 >:0 {N}:n {I}:u n:n

"Voicing of non-coronal stem-final voiceless consonants"
Cx:Cy <=> :Vowel/:0* _ %>: :Vowel/:0* ;
      except
                     _ %{☭%}: %>: ; 
      where Cx in ( p k t )
            Cy in ( b g d )
      matched ;



! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

